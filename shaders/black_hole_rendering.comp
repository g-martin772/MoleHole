#version 460 core

layout(local_size_x = 16, local_size_y = 16) in;
layout(rgba32f, binding = 0) uniform image2D l_outputImage;

// textures
uniform sampler2D u_skyboxTexture;
uniform sampler2D u_blackbodyLUT; // 2D LUT: x=temperature, y=redshift factor
uniform sampler2D u_accelerationLUT; // 2D LUT: x=angular momentum squared, y=radius
uniform sampler2D u_hrDiagramLUT; // 1D LUT: mass -> temperature, luminosity, radius

// constants
const float EPSILON = 0.01;
const float STEP_SIZE = 1.0;
const float PI = 3.1415926535;

// black holes
uniform int u_numBlackHoles;
uniform vec3 u_blackHolePositions[8];
uniform float u_blackHoleMasses[8];
uniform float u_blackHoleSpins[8]; // 0.0 to 1.0
uniform vec3 u_blackHoleSpinAxes[8]; // normalized spin axis

// spheres
const int MAX_SPHERES = 16;
uniform int u_numSpheres;
uniform vec3 u_spherePositions[MAX_SPHERES];
uniform float u_sphereRadii[MAX_SPHERES];
uniform vec4 u_sphereColors[MAX_SPHERES];
uniform float u_sphereMasses[MAX_SPHERES]; // In solar masses for HR diagram
uniform int u_renderSpheres = 1;

// debug
uniform int u_debugMode = 0;

// time
uniform float u_time;

// camera
uniform vec3 u_cameraPos;
uniform vec3 u_cameraFront;
uniform vec3 u_cameraUp;
uniform vec3 u_cameraRight;
uniform float u_fov;
uniform float u_aspect;

// settings
uniform int u_gravitationalLensingEnabled = 1;
uniform int u_accretionDiskEnabled = 1;
uniform int u_renderBlackHoles = 1;
uniform float u_accDiskHeight = 0.2;
uniform float u_accDiskNoiseScale = 1.0;
uniform float u_accDiskNoiseLOD = 5.0;
uniform float u_accDiskSpeed = 0.5;
uniform float u_dopplerBeamingEnabled = 1.0;
uniform int u_useAccelerationLUT = 1;
uniform int u_useHRDiagramLUT = 1;

// new uniforms for disk temperature and GR redshift toggle
uniform float u_accDiskTemp = 2000.0;
uniform int u_gravitationalRedshiftEnabled = 1;

// LUT parameters (must match the LUT texture generation)
// Using uniforms instead of const to avoid GLSL type inference issues
uniform float u_lutTempMin = 1000.0;
uniform float u_lutTempMax = 40000.0;
uniform float u_lutRedshiftMin = 0.1;
uniform float u_lutRedshiftMax = 3.0;

// Acceleration LUT parameters (must match AccelerationLUTGenerator constants)
const float ACC_LUT_R_MIN = 0.01;
const float ACC_LUT_R_MAX = 50.0;
const float ACC_LUT_ANG_MOM_MIN = 0.0;
const float ACC_LUT_ANG_MOM_MAX = 100.0;
const float ACC_LUT_LOG_R_MIN = log(ACC_LUT_R_MIN);
const float ACC_LUT_LOG_R_MAX = log(ACC_LUT_R_MAX);

// HR Diagram LUT parameters (must match HRDiagramLUTGenerator constants)
const float HR_MASS_MIN = 0.08;
const float HR_MASS_MAX = 100.0;
const float HR_LOG_MASS_MIN = log(HR_MASS_MIN);
const float HR_LOG_MASS_MAX = log(HR_MASS_MAX);

vec3 getBlackbodyColorLUT(float temperature, float redshiftFactor) {
    // Map temperature to [0,1] range for LUT lookup
    float tempRange = u_lutTempMax - u_lutTempMin;
    float tempNorm = (temperature - u_lutTempMin) / tempRange;
    tempNorm = max(0.0f, min(1.0f, tempNorm));
    
    // Map redshift factor to [0,1] range for LUT lookup
    float redshiftRange = u_lutRedshiftMax - u_lutRedshiftMin;
    float redshiftNorm = (redshiftFactor - u_lutRedshiftMin) / redshiftRange;
    redshiftNorm = max(0.0f, min(1.0f, redshiftNorm));
    
    // Sample the LUT (bilinear interpolation happens automatically)
    vec3 color = texture(u_blackbodyLUT, vec2(tempNorm, redshiftNorm)).rgb;
    
    return color;
}

float calculateRedShift(vec3 pos)
{
    float dist = sqrt(dot(pos, pos));
    float valid = step(1.0, dist);

    float redshift = sqrt(max(0.0, 1.0 - 1.0 / max(dist, 1.0))) - 1.0;
    redshift = 1.0 / (1.0 + redshift);

    redshift *= valid;
    return redshift;
}

float calculateDopplerEffect(vec3 pos, vec3 viewDir)
{
    vec3 vel;
    float r = sqrt(dot(pos, pos));
    float safeR = max(r, 1.0 + 1e-6);

    if (r < 1.0) return 1.0;

    float velMag0 = -sqrt((1.0 * 1.0 / (2.0 * (safeR - 1.0))));
    float velMag1 = -sqrt((1.0 * 1.0 / r) * (1.0 - 3.0 * 1.0 / (safeR)));

    float velMag = velMag0; // choose the simpler Keplerian fallback

    vec3 velDir = normalize(cross(vec3(0.0, 1.0, 0.0), pos));
    vel = velDir * velMag;

    vec3 beta_s = vel; // c normalized to 1 in this shader context

    float gamma = inversesqrt(max(1e-12, 1.0 - dot(beta_s, beta_s)));
    float dopplerShift = gamma * (1.0 + dot(vel, normalize(viewDir)));

    return max(0.0001, dopplerShift);
}


// calculation methods
float calculateEventHorizonRadius(float mass) {
    return 2.0f * mass;
}

float calculateInfluenceRadius(float r_s) {
    return 5.0f * r_s;
}

vec3 calculateAngularMomentumFromSpin(float spin, vec3 spinAxis, float mass) {
    float h = spin * mass;
    return h * normalize(spinAxis);
}

// Get temperature from black hole mass using HR diagram
float getTemperatureFromMass(float mass) {
    if (u_useHRDiagramLUT == 1) {
        // Map mass to [0,1] using log scale (matching the generator)
        float logMass = log(clamp(mass, HR_MASS_MIN, HR_MASS_MAX));
        float massNorm = (logMass - HR_LOG_MASS_MIN) / (HR_LOG_MASS_MAX - HR_LOG_MASS_MIN);
        massNorm = clamp(massNorm, 0.0, 1.0);
        
        // Sample the LUT (returns vec3: temperature, luminosity, radius)
        // We only use the temperature (r channel)
        vec3 hrData = texture(u_hrDiagramLUT, vec2(massNorm, 0.5)).rgb;
        return hrData.r; // temperature
    } else {
        // Fallback: use the uniform temperature
        return u_accDiskTemp;
    }
}

vec3 calculateAccelerationLUT(float angMomentumSqrd, vec3 relPos) {
    float rSqrd = dot(relPos, relPos);
    float r = sqrt(rSqrd);
    
    // Map angular momentum squared to [0,1] for LUT lookup
    float angMomNorm = (angMomentumSqrd - ACC_LUT_ANG_MOM_MIN) / (ACC_LUT_ANG_MOM_MAX - ACC_LUT_ANG_MOM_MIN);
    angMomNorm = clamp(angMomNorm, 0.0, 1.0);
    
    // Map radius to [0,1] using log scale (matching the generator)
    float logR = log(max(r, ACC_LUT_R_MIN));
    float rNorm = (logR - ACC_LUT_LOG_R_MIN) / (ACC_LUT_LOG_R_MAX - ACC_LUT_LOG_R_MIN);
    rNorm = clamp(rNorm, 0.0, 1.0);
    
    // Sample the LUT (x = angular momentum, y = radius)
    float factor = texture(u_accelerationLUT, vec2(angMomNorm, rNorm)).r;
    
    return factor * relPos;
}
vec3 toSpherical(vec3 pos) {
    float rho = sqrt(pow(pos.x, 2) + pow(pos.y, 2) + pow(pos.z, 2));
    float theta = atan(pos.z, pos.x);
    float phi = asin(pos.y / rho);
    return vec3(rho, theta, phi);
}
vec2 directionToSpherical(vec3 direction) {
    vec3 d = normalize(direction);
    float theta = atan(d.z, d.x);
    float phi = asin(d.y);

    float u = (theta + PI) / (2.0 * PI);
    float v = (phi + PI * 0.5) / PI;

    return vec2(u, v);
}

// noise functions
vec4 permute(vec4 x) { return mod(((x * 34.0) + 1.0) * x, 289.0); }
vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
float snoise(vec3 v) {
    const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
    const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

    vec3 i = floor(v + dot(v, C.yyy));
    vec3 x0 = v - i + dot(i, C.xxx);

    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min(g.xyz, l.zxy);
    vec3 i2 = max(g.xyz, l.zxy);
    
    vec3 x1 = x0 - i1 + 1.0 * C.xxx;
    vec3 x2 = x0 - i2 + 2.0 * C.xxx;
    vec3 x3 = x0 - 1. + 3.0 * C.xxx;
    
    i = mod(i, 289.0);
    vec4 p = permute(permute(permute(i.z + vec4(0.0, i1.z, i2.z, 1.0)) + i.y + vec4(0.0, i1.y, i2.y, 1.0)) + i.x + vec4(0.0, i1.x, i2.x, 1.0));

    float n_ = 1.0 / 7.0;
    vec3 ns = n_ * D.wyz - D.xzx;

    vec4 j = p - 49.0 * floor(p * ns.z * ns.z);

    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_);

    vec4 x = x_ * ns.x + ns.yyyy;
    vec4 y = y_ * ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);

    vec4 b0 = vec4(x.xy, y.xy);
    vec4 b1 = vec4(x.zw, y.zw);

    vec4 s0 = floor(b0) * 2.0 + 1.0;
    vec4 s1 = floor(b1) * 2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));

    vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
    vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

    vec3 p0 = vec3(a0.xy, h.x);
    vec3 p1 = vec3(a0.zw, h.y);
    vec3 p2 = vec3(a1.xy, h.z);
    vec3 p3 = vec3(a1.zw, h.w);

    vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;

    vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
    m = m * m;
    return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}

// Ray-sphere intersection
// Returns true if intersection found, outputs distance t
bool intersectSphere(vec3 rayOrigin, vec3 rayDir, vec3 sphereCenter, float radius, out float t) {
    vec3 oc = rayOrigin - sphereCenter;
    float b = dot(oc, rayDir);
    float c = dot(oc, oc) - radius * radius;
    float discriminant = b * b - c;
    
    if (discriminant < 0.0) return false;
    
    float sqrtD = sqrt(discriminant);
    float t0 = -b - sqrtD;
    float t1 = -b + sqrtD;
    
    // Return nearest positive intersection
    if (t0 > EPSILON) {
        t = t0;
        return true;
    }
    if (t1 > EPSILON) {
        t = t1;
        return true;
    }
    
    return false;
}

// Render a sphere with lighting and HR diagram color
vec3 renderSphere(vec3 hitPoint, vec3 rayDir, int sphereIndex, vec3 lightDir) {
    vec3 sphereCenter = u_spherePositions[sphereIndex];
    vec3 normal = normalize(hitPoint - sphereCenter);
    
    // Get base color from HR diagram if mass is valid
    vec3 baseColor = u_sphereColors[sphereIndex].rgb;
    float mass = u_sphereMasses[sphereIndex];
    
    if (u_useHRDiagramLUT == 1 && mass > 0.0) {
        float temp = getTemperatureFromMass(mass);
        if (temp > 0.0) {
            // Map temperature to [0,1] range for LUT lookup
            float tempRange = u_lutTempMax - u_lutTempMin;
            float tempNorm = (temp - u_lutTempMin) / tempRange;
            tempNorm = clamp(tempNorm, 0.0, 1.0);
            
            // Use redshift = 1.0 (no redshift) for static spheres
            float redshiftNorm = (1.0 - u_lutRedshiftMin) / (u_lutRedshiftMax - u_lutRedshiftMin);
            redshiftNorm = clamp(redshiftNorm, 0.0, 1.0);
            
            // Sample the blackbody LUT
            baseColor = texture(u_blackbodyLUT, vec2(tempNorm, redshiftNorm)).rgb;
        }
    }
    
    // Simple diffuse + ambient lighting
    float diffuse = max(dot(normal, lightDir), 0.0);
    float ambient = 0.3;
    
    vec3 color = baseColor * (ambient + diffuse * 0.7);
    
    return color;
}

void adiskColor(vec3 pos, inout vec3 color, inout float alpha, float eventHorizonRadius, vec3 rayOrigin, float blackHoleMass) {    
    
    float iscoRadius = 2.0f * eventHorizonRadius;
    float outerRadius = 5.0f * eventHorizonRadius;
    float distanceFromCenter = length(pos);

    if (distanceFromCenter < iscoRadius) return;
    
    float density = max(0.0, 1.0 - length(pos.xyz / vec3(outerRadius, u_accDiskHeight, outerRadius)));
    
    if (density < EPSILON) return;  
    
    vec3 sphericalCoord = toSpherical(pos);

    // compute noise-driven pattern
    float noise = 1.0;
    for (int i = 0; i < int(u_accDiskNoiseLOD); i++) {
        noise *= 0.5 * snoise(sphericalCoord * pow(max(1, i), 2) * u_accDiskNoiseScale) + 0.5;
        if (i % 2 == 0) {
            sphericalCoord.y += u_time * u_accDiskSpeed;
        } else {
            sphericalCoord.y -= u_time * u_accDiskSpeed;
        }
    }
    
    // Temperature-based blackbody with doppler + gravitational redshift
    float grFactor = 1.0;
    if (u_gravitationalRedshiftEnabled == 1) {
        // scale the position by the event horizon radius so calculateRedShift expects r~1 at horizon
        grFactor = calculateRedShift(pos / max(1e-6, eventHorizonRadius));
        grFactor = max(grFactor, 1e-6);
    }

    float doppler = 1.0;
    if (u_dopplerBeamingEnabled > 0.5) {
        vec3 viewDir = normalize(rayOrigin - (pos + u_cameraPos));
        // pos is relative to BH center; calculateDopplerEffect expects pos in same units as calculateRedShift
        doppler = calculateDopplerEffect(pos / max(1e-6, eventHorizonRadius), viewDir);
    }

    float dopplerRedshift = 1.0 / mix(1.0, doppler * grFactor, float(u_dopplerBeamingEnabled > 0.5));

    // Use HR diagram to get temperature from black hole mass
    float temp = getTemperatureFromMass(blackHoleMass);
    temp = max(100.0, temp);
    
    vec3 bbColor = getBlackbodyColorLUT(temp, dopplerRedshift);
    
    color += bbColor * alpha * abs(noise);
}


vec3 rayMarching(vec3 rayOrigin, vec3 rayDirection) {
 
    bool hitEventHorizon = false;
    vec3 color = vec3(0.0f);
    float alpha = 1.0f;
    
    float stepSize = STEP_SIZE;
    
    for (int i = 0; i < 200; i++) {
        if (u_renderBlackHoles == 1) {
            
            // Reset step size to base value each iteration
            float currentStepSize = stepSize;
            
            for (int j = 0; j < u_numBlackHoles; j++) {
                vec3 relativePos = rayOrigin - u_blackHolePositions[j];
                float distanceToBH = length(relativePos);
                
                // Calculate orbital angular momentum of the light ray
                vec3 orbitalAngMomentum = cross(relativePos, rayDirection);
                
                // Get the black hole's intrinsic angular momentum from its spin
                vec3 bhAngMomentum = calculateAngularMomentumFromSpin(
                    u_blackHoleSpins[j], 
                    u_blackHoleSpinAxes[j], 
                    u_blackHoleMasses[j]
                );
                
                // Total angular momentum: orbital + contribution from frame-dragging
                // For simplicity, we use the orbital component's magnitude modified by spin
                vec3 totalAngMomentum = orbitalAngMomentum + bhAngMomentum * 0.1; // Scale factor for frame-dragging effect
                float angMomSqrd = dot(totalAngMomentum, totalAngMomentum);
                
                float r_s = calculateEventHorizonRadius(u_blackHoleMasses[j]);
                float r_i = calculateInfluenceRadius(r_s);
                
                if (distanceToBH > r_i) {
                    continue;
                }
                
                // Adaptive step sizing based on distance to event horizon
                currentStepSize *= min(1.0, distanceToBH / r_s);
                
                if (u_gravitationalLensingEnabled == 1) {
                    vec3 acceleration = calculateAccelerationLUT(angMomSqrd, relativePos);
                    
                    rayDirection += acceleration * u_blackHoleMasses[j] * currentStepSize;
                    rayDirection = normalize(rayDirection);
                }
                
                // Add accretion disk color for current position (relative to this black hole)
                if (u_accretionDiskEnabled == 1) {
                    adiskColor(relativePos, color, alpha, r_s, rayOrigin, u_blackHoleMasses[j]);
                }
                
                // Check for event horizon collision
                if (distanceToBH < r_s) {
                    hitEventHorizon = true;
                    break;
                }                
            }
            
            if (hitEventHorizon) {
                break;
            }
                    
            rayOrigin += rayDirection * currentStepSize;
        }

        // Check for sphere intersections AFTER ray marching
        // This is more efficient than checking every step
        if (u_renderSpheres == 1 && u_numSpheres > 0 && !hitEventHorizon) {
            float closestT = 1e10;
            int closestSphere = -1;

            // Find closest sphere intersection
            for (int i = 0; i < u_numSpheres; i++) {
                float t;
                if (intersectSphere(rayOrigin, rayDirection, u_spherePositions[i], u_sphereRadii[i], t)) {
                    if (t < closestT) {
                        closestT = t;
                        closestSphere = i;
                    }
                }
            }

            // If we hit a sphere, render it and return early
            if (closestSphere >= 0) {
                vec3 hitPoint = rayOrigin + rayDirection * closestT;

                // Simple directional light (can be made into a uniform later)
                vec3 lightDir = normalize(vec3(1.0, 1.0, 1.0));

                color = renderSphere(hitPoint, rayDirection, closestSphere, lightDir);
            }
        }
    }
    
    // return bent light rays
    if (!hitEventHorizon)
        color += texture(u_skyboxTexture, directionToSpherical(rayDirection)).rgb * alpha;
    if (length(color) < EPSILON)
        return vec3(0.0f);
    return color;
}


void main() {
    // Get the pixel coordinates and imageSize
    ivec2 texCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imageSize = imageSize(l_outputImage);
    
    // if image is smaller than the viewport, return
    if (texCoords.x >= imageSize.x || texCoords.y >= imageSize.y) {
        return;
    }
    
    vec2 uv = (vec2(texCoords) + 0.5f) / vec2(imageSize);
    uv = uv * 2.0f - 1.0f;
    uv.x *= u_aspect;
    
    float tanHalfFov = tan(radians(u_fov) * 0.5);
    vec3 rayDir = normalize(u_cameraFront + u_cameraRight * uv.x * tanHalfFov + u_cameraUp * uv.y * tanHalfFov);
    vec3 rayOrigin = u_cameraPos;
    
    // Perform ray marching to get the color.
    vec3 color = rayMarching(rayOrigin, rayDir);
    
    // Generate Output Image
    imageStore(l_outputImage, texCoords, vec4(color, 1.0f));   
}