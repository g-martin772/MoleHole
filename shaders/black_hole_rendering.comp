#version 460 core

layout(local_size_x = 16, local_size_y = 16) in;
layout(rgba32f, binding = 0) uniform image2D l_outputImage;

// textures
uniform sampler2D u_skyboxTexture;

// constants
const float EPSILON = 0.01;
const float STEP_SIZE = 0.1;
const float PI = 3.1415926535;

// black holes
uniform int u_numBlackHoles;
uniform vec3 u_blackHolePositions[8];
uniform float u_blackHoleMasses[8];

// debug
uniform int u_debugMode = 0;

// time
uniform float u_time;

// camera
uniform vec3 u_cameraPos;
uniform vec3 u_cameraFront;
uniform vec3 u_cameraUp;
uniform vec3 u_cameraRight;
uniform float u_fov;
uniform float u_aspect;

// settings
uniform int u_gravitationalLensingEnabled = 1;
uniform int u_accretionDiskEnabled = 1;
uniform int u_renderBlackHoles = 1;
uniform float u_accDiskParticle = 1.0;
uniform float u_accDiskHeight = 0.2;
uniform float u_accDiskLit = 0.5;
uniform float u_accDiskDensityV = 1.0;
uniform float u_accDiskDensityH = 1.0;
uniform float u_accDiskNoiseScale = 1.0;
uniform float u_accDiskNoiseLOD = 5.0;
uniform float u_accDiskSpeed = 0.5;
uniform float u_dopplerBeamingEnabled = 1.0;
uniform float u_dopplerBeamingIntensity = 2.0;


// calculation methods
float calculateEventHorizonRadius(float mass) {
    return 2.0f * mass;
}
vec3 calculateAcceleration(float angMomentumSqrd, vec3 relPos) {
    float r5 = pow(dot(relPos, relPos), 2.5);
    
    // avoid division by zero
    if (r5 < EPSILON)
        return (-1.5f * angMomentumSqrd * relPos) / (1.0f * EPSILON);
    
    return (-1.5f * angMomentumSqrd * relPos) / (1.0f * r5);
}
vec3 toSpherical(vec3 pos) {
    float rho = sqrt(pow(pos.x, 2) + pow(pos.y, 2) + pow(pos.z, 2));
    float theta = atan(pos.z, pos.x);
    float phi = asin(pos.y / rho);
    return vec3(rho, theta, phi);
}
vec2 directionToSpherical(vec3 direction) {
    vec3 d = normalize(direction);
    float theta = atan(d.z, d.x);
    float phi = asin(d.y);

    float u = (theta + PI) / (2.0 * PI);
    float v = (phi + PI * 0.5) / PI;

    return vec2(u, v);
}


// noise functions
vec4 permute(vec4 x) { return mod(((x * 34.0) + 1.0) * x, 289.0); }
vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
float snoise(vec3 v) {
    const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
    const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

    vec3 i = floor(v + dot(v, C.yyy));
    vec3 x0 = v - i + dot(i, C.xxx);

    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min(g.xyz, l.zxy);
    vec3 i2 = max(g.xyz, l.zxy);
    
    vec3 x1 = x0 - i1 + 1.0 * C.xxx;
    vec3 x2 = x0 - i2 + 2.0 * C.xxx;
    vec3 x3 = x0 - 1. + 3.0 * C.xxx;
    
    i = mod(i, 289.0);
    vec4 p = permute(permute(permute(i.z + vec4(0.0, i1.z, i2.z, 1.0)) + i.y + vec4(0.0, i1.y, i2.y, 1.0)) + i.x + vec4(0.0, i1.x, i2.x, 1.0));

    float n_ = 1.0 / 7.0;
    vec3 ns = n_ * D.wyz - D.xzx;

    vec4 j = p - 49.0 * floor(p * ns.z * ns.z);

    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_);

    vec4 x = x_ * ns.x + ns.yyyy;
    vec4 y = y_ * ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);

    vec4 b0 = vec4(x.xy, y.xy);
    vec4 b1 = vec4(x.zw, y.zw);

    vec4 s0 = floor(b0) * 2.0 + 1.0;
    vec4 s1 = floor(b1) * 2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));

    vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
    vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

    vec3 p0 = vec3(a0.xy, h.x);
    vec3 p1 = vec3(a0.zw, h.y);
    vec3 p2 = vec3(a1.xy, h.z);
    vec3 p3 = vec3(a1.zw, h.w);

    vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;

    vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
    m = m * m;
    return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}
void adiskColor(vec3 pos, inout vec3 color, inout float alpha, float eventHorizonRadius, vec3 rayOrigin) {    
    
    float iscoRadius = 2.0 * eventHorizonRadius;
    float outerRadius = 8.0 * eventHorizonRadius;
    float distanceFromCenter = length(pos);

    if (distanceFromCenter < iscoRadius) {
        return;
    }

    float iscoTransitionZone = iscoRadius * 1.2;
    float iscoFactor = smoothstep(iscoRadius, iscoTransitionZone, distanceFromCenter);
    float density = max(0.0, 1.0 - length(pos.xyz / vec3(outerRadius, u_accDiskHeight, outerRadius)));
    if (density < EPSILON) return;

    density *= pow(1.0 - abs(pos.y) / u_accDiskHeight, u_accDiskDensityV);

    // Apply ISCO factor to create smooth transition
    density *= iscoFactor;
    if (density < EPSILON) return;

    vec3 sphericalCoord = toSpherical(pos);

    // Scale the rho and phi so that the particles appear to be at the correct scale visually
    sphericalCoord.y *= 10.0;
    sphericalCoord.z *= 20.0;

    density *= 1.0 / pow(sphericalCoord.x, u_accDiskDensityH);
    density *= 5000.0;

    if (u_accDiskParticle < 0.5) {
        color += vec3(0.0, 1.0, 0.0) * density * 0.02;
        return;
    }

    vec3 dustColor = vec3(0.8f, 0.3f, 0.3f);

    float noise = 1.0;
    for (int i = 0; i < int(u_accDiskNoiseLOD); i++) {
        noise *= 0.5 * snoise(sphericalCoord * pow(i, 2) * u_accDiskNoiseScale) + 0.5;
        if (i % 2 == 0) {
            sphericalCoord.y += u_time * u_accDiskSpeed;
        } else {
            sphericalCoord.y -= u_time * u_accDiskSpeed;
        }
    }
    
    // Doppler beaming effect
    float dopplerBrightness = 1.0;
    vec3 dopplerColor = dustColor;
    
    if (u_dopplerBeamingEnabled > 0.5) {
        // Calculate disk velocity (Keplerian orbit)
        // Velocity is perpendicular to radial direction in the disk plane
        vec3 diskNormal = vec3(0.0, 1.0, 0.0); // assuming disk in XZ plane
        vec3 radialDir = normalize(vec3(pos.x, 0.0, pos.z));
        vec3 velocityDir = cross(diskNormal, radialDir);
        
        // Keplerian velocity: v ~ 1/sqrt(r)
        float orbitalSpeed = 1 / sqrt(distanceFromCenter / eventHorizonRadius);
        orbitalSpeed = clamp(orbitalSpeed, 0.0, 0.9); // Cap at 0.9c for stability
        
        vec3 velocity = velocityDir * orbitalSpeed;
        
        // Calculate direction to observer
        vec3 toObserver = normalize(rayOrigin - pos);
        
        // Doppler factor: cosine of angle between velocity and observer
        float beta = dot(velocity, toObserver);
        
        // Relativistic doppler factor
        float gamma = 1.0 / sqrt(1.0 - orbitalSpeed * orbitalSpeed);
        float dopplerFactor = 1.0 / (gamma * (1.0 - beta));
        
        // Beaming effect: intensity ~ (doppler factor)^(3+alpha)
        // Using alpha=0 for simplicity (gives ^3)
        dopplerBrightness = pow(dopplerFactor, u_dopplerBeamingIntensity);
        
        // Color shift due to doppler effect
        // Approaching side -> blue shift (beta > 0)
        // Receding side -> red shift (beta < 0)
        float colorShift = beta * orbitalSpeed;
        
        if (colorShift > 0.0) {
            // Blue shift for approaching material
            dopplerColor = mix(dustColor, vec3(0.3, 0.5, 1.0), colorShift);
            alpha = 0.4;
        } else {
            // Red shift for receding material
            dopplerColor = mix(dustColor, vec3(1.0, 0.2, 0.1), abs(colorShift));
        }
    }
    
    color += density * dopplerColor * alpha * abs(noise);
}


vec3 rayMarching(vec3 rayOrigin, vec3 rayDirection) {
 
    bool hitEventHorizon = false;
    vec3 color = vec3(0.0f);
    float alpha = 1.0f;
    
    float stepSize = STEP_SIZE;
    
    for (int i = 0; i < 2500; i++) {
        if (u_renderBlackHoles == 1) {
            
            // Reset step size to base value each iteration
            float currentStepSize = stepSize;
            
            for (int j = 0; j < u_numBlackHoles; j++) {
                vec3 relativePos = rayOrigin - u_blackHolePositions[j];
                float distanceToBH = length(relativePos);
                vec3 angMomentum = cross(relativePos, rayDirection);
                float r_s = calculateEventHorizonRadius(u_blackHoleMasses[j]);
                
                // Adaptive step sizing based on distance to event horizon
                currentStepSize *= min(1.0, distanceToBH / r_s);
                
                if (u_gravitationalLensingEnabled == 1) {
                    rayDirection += calculateAcceleration(dot(angMomentum, angMomentum), relativePos) * u_blackHoleMasses[j] * 2.0f * currentStepSize;
                    rayDirection = normalize(rayDirection);
                }

                // Add accretion disk color for current position (relative to this black hole)
                if (u_accretionDiskEnabled == 1) {
                    adiskColor(relativePos, color, alpha, r_s, rayOrigin);
                }
                
                // Check for event horizon collision
                if (distanceToBH < r_s) {
                    hitEventHorizon = true;
                    break;
                }                
            }
            
            if (hitEventHorizon) {
                break;
            }
                    
            rayOrigin += rayDirection * currentStepSize;
        }
    }
    
    // return bent light rays
    if (!hitEventHorizon)
        color += texture(u_skyboxTexture, directionToSpherical(rayDirection)).rgb * alpha;
    if (length(color) < EPSILON)
        return vec3(0.0f);
    return color;
}


void main() {
    // Get the pixel coordinates and imageSize
    ivec2 texCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imageSize = imageSize(l_outputImage);
    
    // if image is smaller than the viewport, return
    if (texCoords.x >= imageSize.x || texCoords.y >= imageSize.y) {
        return;
    }
    
    vec2 uv = (vec2(texCoords) + 0.5f) / vec2(imageSize);
    uv = uv * 2.0f - 1.0f;
    uv.x *= u_aspect;
    
    float tanHalfFov = tan(radians(u_fov) * 0.5);
    vec3 rayDir = normalize(u_cameraFront + u_cameraRight * uv.x * tanHalfFov + u_cameraUp * uv.y * tanHalfFov);
    vec3 rayOrigin = u_cameraPos;
    
    // Perform ray marching to get the color.
    vec3 color = rayMarching(rayOrigin, rayDir);
    
    // Generate Output Image
    imageStore(l_outputImage, texCoords, vec4(color, 1.0f));   
}